#!/bin/env zsh

# Python Environment Info

function update_python_info {
    local virtualenv_format
    local virtualenv_formatted
    local pyenv_format
    local pyenv_formatted

    # clean up prev $python_info
    unset python_info
    typeset -gA python_info

    # Format virtualenv
    if [[ -n "$VIRTUAL_ENV" ]]; then
      zstyle -s ':python:info:virtualenv' format 'virtualenv_format'
      zformat -f virtualenv_formatted "$virtualenv_format" "v:${VIRTUAL_ENV:t}"
      python_info[virtualenv]="$virtualenv_formatted"
    fi

    # Format pyenv
    if (( $+commands[pyenv] )); then
      zstyle -s ':python:info:pyenv' format 'pyenv_format'
      zformat -f pyenv_formatted "$pyenv_format" "p:$(pyenv version-name)"
      python_info[pyenv]="$pyenv_formatted"
    fi
}

update_python_info "$@"