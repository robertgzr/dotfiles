# Provide a minimal prompt interface

# Sets up: pwd
# takes 'short' as argument

_prompt_minimal_start_time=$SECONDS

function prompt_dir() {
    if [[ $1 = short ]]; then
        # shortend path behaviour
        local pwd="${PWD/#$HOME/~}"

        if [[ "$pwd" == (#m)[/~] ]]; then
            prompt_pwd="$MATCH"
            unset MATCH
        else
            prompt_pwd="$FG[002]$FX[no-bold]$FX[italic]${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
        fi
    else
        # default behaviour
        prompt_pwd=" %~%<< "
    fi
}

function prompt_paradox_print_elapsed_time {
  local end_time=$(( SECONDS - _prompt_minimal_start_time ))
  local hours minutes seconds remainder

  if (( end_time >= 3600 )); then
    hours=$(( end_time / 3600 ))
    remainder=$(( end_time % 3600 ))
    minutes=$(( remainder / 60 ))
    seconds=$(( remainder % 60 ))
    print -P "%B%F{red}>>> elapsed time ${hours}h${minutes}m${seconds}s%b"
  elif (( end_time >= 60 )); then
    minutes=$(( end_time / 60 ))
    seconds=$(( end_time % 60 ))
    print -P "%B%F{yellow}>>> elapsed time ${minutes}m${seconds}s%b"
  elif (( end_time > 10 )); then
    print -P "%B%F{green}>>> elapsed time ${end_time}s%b"
  fi
}

# ==== Prompt constructor functions

function prompt_precmd {
    prompt_dir short

    autoload -Uz update_git_info
    if (( $+functions[update_git_info] )); then
        update_git_info
    fi

    PROMPT='$FG[002]$prompt_pwd%{%f%}$FX[reset] $FG[255]> '

    if [[ -n "$FORMAT_GIT_INFO" ]]; then
        RPROMPT='$FORMAT_GIT_INFO[status]'
    fi

    # Calc elapsed time
    prompt_paradox_print_elapsed_time
}

function prompt_preexec {
    _prompt_minimal_elapsed_time="$SECONDS"
}

function prompt_minimal {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    autoload -Uz add-zsh-hook

    zstyle ':git:info' verbose 'yes'

    zstyle ':git:info:commit' format "$FG[000]@$FX[reset]%c"
    zstyle ':git:info:branch' format "$FG[000]$FG[004]%b$FX[reset]"

    zstyle ':git:info:ahead' format " ↑"
    zstyle ':git:info:behind' format "↓"

    zstyle ':git:info:clean' format " $FG[002]✔$FX[reset]"
    zstyle ':git:info:dirty' format " $FG[001]✗$FX[reset]"
    zstyle ':git:info:unmerged' format "═"
    zstyle ':git:info:modified' format "✱"
    zstyle ':git:info:untracked' format "?"

    zstyle ':git:info:keys' format \
        'status' "%b%c%A%B%C%D%m%U%u $FG[000]" \
        'short' '%b%C%D $FG[000]'

    add-zsh-hook preexec prompt_preexec
    add-zsh-hook precmd prompt_precmd
}

prompt_minimal "$@"